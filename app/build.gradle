apply plugin: 'com.android.application'

String VERSION_NAME = "1.0.0"

/**
 * 初始化版本号
 * @param str
 * @return
 */
int getVersionCode(String str) {
    int buildID = 0;
    String[] array = str.split("\\.").toList();
    int major = array[0].toInteger();
    int minor = array[1].toInteger();
    int release = array[2].toInteger();
    if(array.length > 3) {
        buildID = array[3].toInteger();
    }
    int code = major * 1000000 + minor * 10000 + release * 100  + buildID * 1;
    return code;
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.xuhongchuan.axenote"
        minSdkVersion 15
        targetSdkVersion 23
        versionName VERSION_NAME
        versionCode getVersionCode(VERSION_NAME)

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        // 读取local.properties
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        // 读取签名
        keystore {
            storeFile file(properties.getProperty("STORE_FILE"))
            storePassword properties.getProperty("STORE_PASSWORD")
            keyAlias properties.getProperty("KEY_ALIAS")
            keyPassword properties.getProperty("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            // 显示log
            buildConfigField "boolean", "LOG_ENABLE", "true"

            // 签名
            signingConfig signingConfigs.keystore
        }

        release {
            // 不显示log
            buildConfigField "boolean", "LOG_ENABLE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 签名
            signingConfig signingConfigs.keystore
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'

    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'

    // 圆形头像
    // https://github.com/hdodenhof/CircleImageView
    compile 'de.hdodenhof:circleimageview:2.0.0'

    // 主题切换框架
    // https://github.com/StylingAndroid/Prism
    // https://blog.leancloud.cn/3612/
    compile 'com.stylingandroid.prism:prism:1.0.1'

    // Bugly
    // https://bugly.qq.com/androidfast
    // https://github.com/vell001/BetaSDKDemo
    compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
}
