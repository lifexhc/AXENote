apply plugin: 'com.android.application'

String VERSION_NAME = "1.0.0"

/**
 * 初始化版本号
 * @param str
 * @return
 */
static int getVersionCode(String str) {
    int buildID = 0;
    String[] array = str.split("\\.").toList();
    int major = array[0].toInteger();
    int minor = array[1].toInteger();
    int release = array[2].toInteger();
    if(array.length > 3) {
        buildID = array[3].toInteger();
    }
    int code = major * 1000000 + minor * 10000 + release * 100  + buildID * 1;
    return code;
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.xuhongchuan.axenote"
        minSdkVersion 21
        targetSdkVersion 28
        versionName VERSION_NAME
        versionCode getVersionCode(VERSION_NAME)

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    buildTypes {
        debug {
            // 显示log
            buildConfigField "boolean", "LOG_ENABLE", "true"
        }

        release {
            // 不显示log
            buildConfigField "boolean", "LOG_ENABLE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    testImplementation 'junit:junit:4.12'

    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    implementation "com.jakewharton:butterknife:10.1.0"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.1.0"

    implementation 'com.github.bumptech.glide:glide:4.9.0'

    implementation 'com.blankj:utilcodex:1.24.4'

    implementation "com.tencent.bugly:crashreport_upgrade:1.3.6"
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
}
